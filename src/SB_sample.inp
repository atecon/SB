clear
set verbose off
include SB.gfn

# load Johansen macroeconomic data
open denmark.gdt
scalar NBOOT = 999		# no. of bootstrap iterations
# run OLS with real money as dependent variable, real income and bond
# rate as explanatory variables
list lx = LRY IBO
ols LRM 0 lx --simple
bhat = $coeff		# store coefficient vector
lrmh = $yhat		# store fitted values
uh = {$uhat}		# store residuals as matrix

# Construct bootstrap pseudo-data using rule-of-thumb block size,
# estimate model NBOOT times and store estimated coefficients in the matrix bb
matrix bb = zeros(NBOOT, $ncoeff)

loop boot=1..NBOOT
    matrix ub = SB(uh)	# resample
    series ubs =ub
    series lrmb =lrmh+ubs
    # Note that since ubs is guaranteed to be stationary lrmb and lx
    # cointegrate by construction. With the block bootstrap this is not
    # true, and some of the NBOOT regressions of lrmb on lx may be spurious
    ols lrmb 0 lx --quiet
    bb[boot,] = $coeff'
endloop

# take percentiles of distribution of bootstrap estimates
matrix bb05 = quantile(bb, 0.05)
matrix bb95 = quantile(bb, 0.95)
print ""
print "***************************************************************** "
print ""
printf "estimated coefficients %10.2f", bhat'
print ""
print "95% bootstrap confidence intervals"
print ""
printf "Lower limits %15.2f", bb05
printf "Upper limits %15.2f", bb95
print ""
print "***************************************************************** "
